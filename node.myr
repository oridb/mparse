use std

use "types"
use "stab"

pkg mparse =
	/* expr nodes */
	const mkexpr	: (l : srcloc, e : exprop -> expr#)
	const mkblk	: (l : srcloc, st : stab#, stmts : stmt#[:] -> block#)
	const mkif	: (l : srcloc, cond : expr#, iftrue : block#, iffalse : block# -> ifstmt#)
	const mkfunc	: (l : srcloc, st : stab#, ty : tydef#, args : dcl#[:], body : block# -> func#)
	const mkstmt	: (l : srcloc, stmt : stmt -> stmt#)
	const mkloop	: (l : srcloc, init : expr#, cond : expr#, step : expr#, body : block# -> loopstmt#)
	const mkiter	: (l : srcloc, pat : expr#,  seq : expr#, bodk : block# -> iterstmt#)
	const mkmatch	: (l : srcloc, val : expr#, cases : matchcase[:] -> matchstmt#)
	const mktraitdef	: (l : srcloc, name : name, ty : tydef#, auxty : tydef#[:], dcls : dcl#[:] -> traitdef#)
	const mkimpldef	: (l : srcloc, name : name, ty : tydef#, auxty : tydef#[:], dcls : dcl#[:] -> impldef#)

	/* type nodes */
	const mkname	: (name : name, params : tydef#[:] -> tyname#)
	const mktyvar	: (l : srcloc -> tydef#)
	const mkty	: (l : srcloc, ty : ty -> tydef#)
	const tyinit	: (f : file# -> void)

	/* commonly used tytraits and types, initialized in __init__ and then constant */
	var Trnum	: traitdef#
	var Trint	: traitdef#
	var Trflt	: traitdef#
	var Tridx	: traitdef#
	var Trslice	: traitdef#
	var Trfunc	: traitdef#
	var Triter	: traitdef#

	var Tystring	: tydef#
	var Tybool	: tydef#
	var Tyvoidptr	: tydef#

	/* global state */
	var types	: tydef#[:]
	var tymap	: int32[:]
	var tytraits	: std.bitset#[:]
	var traitdefs	: traitdef#[:]
;;

/* global state */
var ntypes	: int32 = 0
var ntraits	: int32 = 0
var tymap
var types
var tytraits
var traitdefs

var Trnum
var Trint
var Trflt
var Tridx
var Trslice
var Trfunc
var Triter

var Tystring
var Tybool
var Tyvoidptr

const tyinit = {f
	var loc, t, tv

	/* the type variable created here is never actually used. */
	tv = mktyvar(loc)
	loc = [.line=-1, .file="internal"]

	/* create tytraits */
	Trnum = mktraitdef(loc, [.ns="", .name="numeric"], tv, [][:], [][:])
	puttrait(f.builtin, Trnum.name, Trnum)
	Trint = mktraitdef(loc, [.ns="", .name="integral"], tv, [][:], [][:])
	puttrait(f.builtin, Trint.name, Trint)
	Trflt = mktraitdef(loc, [.ns="", .name="floating"], tv, [][:], [][:])
	puttrait(f.builtin, Trflt.name, Trflt)
	Tridx = mktraitdef(loc, [.ns="", .name="indexable"], tv, [][:], [][:])
	puttrait(f.builtin, Tridx.name, Tridx)
	Trslice = mktraitdef(loc, [.ns="", .name="sliceable"], tv, [][:], [][:])
	puttrait(f.builtin, Trslice.name, Trslice)
	Trfunc = mktraitdef(loc, [.ns="", .name="function"], tv, [][:], [][:])
	puttrait(f.builtin, Trfunc.name, Trfunc)
	Triter = mktraitdef(loc, [.ns="", .name="iterable"], tv, [][:], [][:])
	puttrait(f.builtin, Triter.name, Triter)

	/* create types */
	t = mkty(loc, `Tyvoid)
	puttype(f.builtin, [.ns="", .name="void"], t)

	t = mkty(loc, `Tybool)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trint.id)
	puttype(f.builtin, [.ns="", .name="bool"], t)
	Tybool = t

	t = mkty(loc, `Tychar)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trint.id)
	puttype(f.builtin, [.ns="", .name="char"], t)

	t = mkty(loc, `Tyint8)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trint.id)
	puttype(f.builtin, [.ns="", .name="int8"], t)

	t = mkty(loc, `Tyint16)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trint.id)
	puttype(f.builtin, [.ns="", .name="int16"], t)

	t = mkty(loc, `Tyint32)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trint.id)
	puttype(f.builtin, [.ns="", .name="int32"], t)

	t = mkty(loc, `Tyint)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trint.id)
	puttype(f.builtin, [.ns="", .name="int"], t)

	t = mkty(loc, `Tyint64)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trint.id)
	puttype(f.builtin, [.ns="", .name="int64"], t)

	/* unsigned integers */
	t = mkty(loc, `Tybyte)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trint.id)
	puttype(f.builtin, [.ns="", .name="byte"], t)

	t = mkty(loc, `Tyuint8)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trint.id)
	puttype(f.builtin, [.ns="", .name="uint8"], t)

	t = mkty(loc, `Tyuint16)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trint.id)
	puttype(f.builtin, [.ns="", .name="uint16"], t)

	t = mkty(loc, `Tyuint32)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trint.id)
	puttype(f.builtin, [.ns="", .name="uint32"], t)

	t = mkty(loc, `Tyuint)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trint.id)
	puttype(f.builtin, [.ns="", .name="uint"], t)

	t = mkty(loc, `Tyuint64)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trint.id)
	puttype(f.builtin, [.ns="", .name="uint64"], t)

	/*floats */
	t = mkty(loc, `Tyflt32)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trflt.id)
	puttype(f.builtin, [.ns="", .name="flt32"], t)

	t = mkty(loc, `Tyflt64)
	std.bsput(tytraits[t.id], Trnum.id)
	std.bsput(tytraits[t.id], Trflt.id)
	puttype(f.builtin, [.ns="", .name="flt64"], t)

	/* a bit odd.. */
	t = mkty(loc, `Tyvalist)
	puttype(f.builtin, [.ns="", .name="..."], t)

	/* global types */
	Tystring = mkty(loc, `Tyslice \
		mkty(loc, `Tybyte))
	Tyvoidptr = mkty(loc, `Typtr \
		mkty(loc, `Tyvoid))
}

const mkexpr = {l, e
	-> std.mk([
		.loc = l,
		.ty = mktyvar(l),
		.e = e,
	])
}

const mkblk = {l, st, stmts
	-> std.mk([
		.loc = l,
		.stab = st,
		.stmts = stmts,
	])
}

const mkfunc = {l, st, ty, args, body
	-> std.mk([
		.loc = l,
		.stab = st,
		.ty = ty,
		.args = args,
		.body = body,
	])
}

const mkstmt = {l, st
	-> std.mk(st)
}

const mkif = {l, cond, iftrue, iffalse
	-> std.mk([
		.cond=cond,
		.iftrue=iftrue,
		.iffalse=iffalse,
	])
}

const mkloop = {l, init, cond, step, blk
	-> std.mk([
		.init=init,
		.cond=cond,
		.step=step,
		.body=blk,
	])
}

const mkiter = {l, pat, iter, blk
	-> std.mk([
		.pat=pat,
		.iter=iter,
		.body=blk
	])
}

const mkmatch = {l, val, cases
	-> std.mk([
		.val=val,
		.cases=cases,
	])
}

const mktraitdef = {l, name, ty, auxty, dcls 
	var tr
	
	tr = std.mk([
		.id=ntraits++,
		.loc=l,
		.name=name,
		.param=ty,
		.aux=auxty,
		.dcls=dcls,
	])
	std.slpush(&traitdefs, tr)
	-> tr
}

const mkimpldef = {l, name, ty, auxty, dcls 
	-> std.mk([
		.loc=l,
		.name=name,
		.ty=ty,
		.aux=auxty,
		.dcls=dcls,
	])
}

const mktyvar = {l
	-> mkty(l, `Tyvar ntypes)
}

const mkty = {l, ty
	var t

	t = std.mk([
		.ty = ty,
		.id = ntypes++,
		.loc = l,
		.fixed = false,
		.found = false,
		.traits = std.mkbs(),
	])
	std.slpush(&types, t)
	std.slpush(&tytraits, std.mkbs())
	std.slpush(&tymap, -1)
	-> t
}


const mkname = {nam, params
	-> std.mk([
		.name=nam,
		.params=params
	])
}
