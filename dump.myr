use std

use "types"
use "errors"

pkg mparse =
;;

const __init__ = {
	var f : file
	var d : dcl
	var st : stab
	var e : expr
	var name : name
	var ty : tydef
	var lit : lit
	var fn : func
	var blk : block
	var loc : srcloc

	std.fmtinstall(std.typeof(&f), fmtfile, [][:])
	std.fmtinstall(std.typeof(&d), fmtdcl, [][:])
	std.fmtinstall(std.typeof(&e), fmtexpr, [][:])
	std.fmtinstall(std.typeof(&st), fmtstab, [][:])
	std.fmtinstall(std.typeof(name), fmtname, [][:])
	std.fmtinstall(std.typeof(&ty), fmttydef, [][:])
	std.fmtinstall(std.typeof(&fn), fmtfn, [][:])
	std.fmtinstall(std.typeof(&blk), fmtblk, [][:])
	std.fmtinstall(std.typeof(lit), fmtlit, [][:])
	std.fmtinstall(std.typeof(loc), fmtloc, [][:])
}

const fmtfile = {sb, ap, opts
	var f : file#
	f = std.vanext(ap)
	filedump(sb, f, 0)
}

const fmtdcl = {sb, ap, opts
	var f : dcl#
	f = std.vanext(ap)
	dcldump(sb, f, 0)
}

const fmtstab = {sb, ap, opts
	var f : stab#
	f = std.vanext(ap)
	stabdump(sb, f, 0)
}

const fmtexpr = {sb, ap, opts
	var f : expr#
	f = std.vanext(ap)
	exprdump(sb, f, 0)
}

const fmtname = {sb, ap, opts
	var n : name

	n = std.vanext(ap)
	namedump(sb, n)
}

const fmttydef = {sb, ap, opts
	var ty : tydef#

	ty = std.vanext(ap)
	tydefdump(sb, ty)
}

const fmtlit = {sb, ap, opts
	var lit : lit

	lit = std.vanext(ap)
	litdump(sb, lit, 0)
}

const fmtfn = {sb, ap, opts
	var fn : func#

	fn = std.vanext(ap)
	fndump(sb, fn, 0)
}

const fmtblk = {sb, ap, opts
	var blk : block#

	blk = std.vanext(ap)
	blkdump(sb, blk, 0, true)
}

const fmtloc = {sb, ap, opts
	var loc : srcloc

	loc = std.vanext(ap)
	std.sbfmt(sb, "{}:{},{}:", loc.file, loc.line, loc.col)
}

const filedump = {sb, f, ind
	ifmt(sb, ind, "file: {}\n", f.path)
	for u in f.uses
		match u
		| `Ulocal l:	ifmt(sb, ind + 1, "local-use:\t{}\n", l)
		| `Ulib l:	ifmt(sb, ind + 1, "lib-use:\t{}\n", l)
		;;
	;;
	ifmt(sb, ind, "libs: {}\n", f.extlibs)
	ifmt(sb, ind, "extlibs: {}\n", f.extlibs)

	for i in f.extinit
		dcldump(sb, i, ind + 1)
	;;

	match f.init
	| `std.Some d:	dcldump(sb, d, ind + 1)
	| `std.None:
	;;

	stabdump(sb, f.builtin, ind + 1)
	stabdump(sb, f.globls, ind + 1)

	for s in f.dcls
		dcldump(sb, s, ind + 1)
	;;
}

const dcldump = {sb, dcl, ind
	ifmt(sb, ind, "Decl {}: {} {}: {}\n", dcl.name, dcl.id, dcl.vis, dcl.ty)
	ifmt(sb, ind + 2, "globl:{}, const:{}, noret:{}\n", \
		dcl.isglobl, dcl.isconst, dcl.isnoret)
	ifmt(sb, ind + 2, "extern:{}, import:{}, hidden:{}\n", \
		dcl.isextern, dcl.isimport, dcl.ishidden)
	ifmt(sb, ind + 2, "pkglocal:{}, generic:{}\n", \
		dcl.ispkglocal, dcl.isgeneric)
	match dcl.init
	| `std.Some e:	exprdump(sb, e, ind + 2)
	| `std.None:	ifmt(sb, ind+2, "<uninitialized>")
	;;
	std.sbfmt(sb, "\n")
}

const exprdump = {sb, e, ind
	match e#
	| [.e = `Onop]:	ifmt(sb, ind, "<nop>")
	| [.e = `Oadd (a, b)]:	ifmt(sb, ind, "({} + {})", a, b)
	| [.e = `Osub (a, b)]:	ifmt(sb, ind, "({} - {})", a, b)
	| [.e = `Omul (a, b)]:	ifmt(sb, ind, "({} * {})", a, b)
	| [.e = `Odiv (a, b)]:	ifmt(sb, ind, "({} / {})", a, b)
	| [.e = `Omod (a, b)]:	ifmt(sb, ind, "({} % {})", a, b)
	| [.e = `Oneg exp]:	ifmt(sb, ind, "(-{})", exp)
	| [.e = `Obor (a, b)]:	ifmt(sb, ind, "({} | {})", a, b)
	| [.e = `Oband (a, b)]:	ifmt(sb, ind, "({} & {})", a, b)
	| [.e = `Obxor (a, b)]:	ifmt(sb, ind, "({} ^ {})", a, b)
	| [.e = `Obsl (a, b)]:	ifmt(sb, ind, "({} << {})", a, b)
	| [.e = `Obsr (a, b)]:	ifmt(sb, ind, "({} >> {})", a, b)
	| [.e = `Obnot exp]:	ifmt(sb, ind, "(~{})", exp)
	| [.e = `Opreinc exp]:	ifmt(sb, ind, "(++{})", exp)
	| [.e = `Opostinc exp]:	ifmt(sb, ind, "({}++)", exp)
	| [.e = `Opredec exp]:	ifmt(sb, ind, "(++{})", exp)
	| [.e = `Opostdec exp]:	ifmt(sb, ind, "({}--)", exp)
	| [.e = `Oaddr exp]:	ifmt(sb, ind, "(&{})", exp)
	| [.e = `Oderef exp]:	ifmt(sb, ind, "({}#)", exp)
	| [.e = `Olor (a, b)]:	ifmt(sb, ind, "({} || {})", a, b)
	| [.e = `Oland (a, b)]:	ifmt(sb, ind, "({} && {})", a, b)
	| [.e = `Olnot exp]:	ifmt(sb, ind, "(!{})", exp)
	| [.e = `Oeq (a, b)]:	ifmt(sb, ind, "({} == {})", a, b)
	| [.e = `One (a, b)]:	ifmt(sb, ind, "({} != {})", a, b)
	| [.e = `Ogt (a, b)]:	ifmt(sb, ind, "({} > {})", a, b)
	| [.e = `Oge (a, b)]:	ifmt(sb, ind, "({} >= {})", a, b)
	| [.e = `Olt (a, b)]:	ifmt(sb, ind, "({} < {})", a, b)
	| [.e = `Ole (a, b)]:	ifmt(sb, ind, "({} <= {})", a, b)
	| [.e = `Oasn (a, b)]:	ifmt(sb, ind, "({} = {})", a, b)
	| [.e = `Oaddeq (a, b)]:	ifmt(sb, ind, "({} += {})", a, b)
	| [.e = `Osubeq (a, b)]:	ifmt(sb, ind, "({} -= {})", a, b)
	| [.e = `Omuleq (a, b)]:	ifmt(sb, ind, "({} *= {})", a, b)
	| [.e = `Odiveq (a, b)]:	ifmt(sb, ind, "({} /= {})", a, b)
	| [.e = `Omodeq (a, b)]:	ifmt(sb, ind, "({} %= {})", a, b)
	| [.e = `Oboreq (a, b)]:	ifmt(sb, ind, "({} |= {})", a, b)
	| [.e = `Obandeq (a, b)]:	ifmt(sb, ind, "({} &= {})", a, b)
	| [.e = `Obxoreq (a, b)]:	ifmt(sb, ind, "({} ^= {})", a, b)
	| [.e = `Obsleq (a, b)]:	ifmt(sb, ind, "({} <<= {})", a, b)
	| [.e = `Obsreq (a, b)]:	ifmt(sb, ind, "({} >>= {})", a, b)
	| [.e = `Oidx (a, b)]:	ifmt(sb, ind, "{}[{}]", a, b)
	| [.e = `Oslice (a, b, c)]:	ifmt(sb, ind, "{}[{} : {}]", a, b, c)
	| [.e = `Omemb (a, memb)]:	ifmt(sb, ind, "{}.{}", a, memb)
	| [.e = `Osize ty]:	ifmt(sb, ind, "sizeof({})", ty)
	| [.e = `Ocast (exp, ty)]:	ifmt(sb, ind, "{} castto({})", exp, ty)
	| [.e = `Oret exp]:	ifmt(sb, ind, "-> {}", exp)
	| [.e = `Ojmp name]:	ifmt(sb, ind, "goto {}", name)
	| [.e = `Obreak]:		ifmt(sb, ind, "break")
	| [.e = `Ocontinue]:	ifmt(sb, ind, "continue")
	| [.e = `Ovar name]:	ifmt(sb, ind, "{}", name)
	| [.e = `Ogap]:		ifmt(sb, ind, "_")
	| [.e = `Olit lit]:	litdump(sb, lit, ind)
	| [.e = `Ocall (f, args)]:	ifmt(sb, ind, "{}({})", f, args)
	| [.e = `Oucon (t, `std.Some v)]:	ifmt(sb, ind, "`{} {}", t, v)
	| [.e = `Oucon (t, `std.None)]:	ifmt(sb, ind, "{}", t)
	| [.e = `Otup al]:	ifmt(sb, ind, "{}", al)
	| [.e = `Ostruct ml]:	ifmt(sb, ind, "{}", ml)
	| [.e = `Oarr il]:	ifmt(sb, ind, "{}", il)
	| [.e = `Oidxlen]:	ifmt(sb, ind, "$")
	;;
}

const stabdump = {sb, st, ind
	var keys

	ifmt(sb, ind, "stab {} {{\n", st.ns)
	keys = std.htkeys(st.syms)
	for k in keys
		match std.get(std.htget(st.syms, k))
		| `Dcl d:	
			ifmt(sb, ind + 1, "{} : {}\n", k, d.ty)
		| `Ucon uc:	
			match uc.ety
			| `std.Some t:	ifmt(sb, ind + 1, "{} : {}\n", k, t)
			| `std.None:	ifmt(sb, ind + 1, "{}\n", k)
			;;
		;;
	;;
	std.slfree(keys)

	keys = std.htkeys(st.types)
	for k in keys
		match std.get(std.htget(st.types, k))
		| `Tyfwd _:	ifmt(sb, ind + 1, "type {} [PROTO]\n", k)
		| `Trfwd _:	ifmt(sb, ind + 1, "trait {} [PROTO]\n", k)
		| `Tydef d:	ifmt(sb, ind + 1, "type {} = {}\n", k, d)
		| `Trdef tr:	ifmt(sb, ind + 1, "trait {} = ...\n", k)
		;;
	;;
	std.slfree(keys)
	ifmt(sb, ind, "}}\n")
}

const namedump = {sb, n
	if n.ns.len > 0
		std.sbfmt(sb, "{}.{}", n.ns, n.name)
	else
		std.sbfmt(sb, "{}", n.name)
	;;
}

const tydefdump = {sb, ty
	match ty.ty
	| `Tyvoid:	std.sbfmt(sb, "void")
	| `Tybool:	std.sbfmt(sb, "bool")
	| `Tychar:	std.sbfmt(sb, "char")
	/* integers */
	| `Tyint8:	std.sbfmt(sb, "int8")
	| `Tyint16:	std.sbfmt(sb, "int16")
	| `Tyint32:	std.sbfmt(sb, "int32")
	| `Tyint:	std.sbfmt(sb, "int")
	| `Tyint64:	std.sbfmt(sb, "int64")
	/* unsigned integers */
	| `Tybyte:	std.sbfmt(sb, "byte")
	| `Tyuint8:	std.sbfmt(sb, "uint8")
	| `Tyuint16:	std.sbfmt(sb, "uint16")
	| `Tyuint32:	std.sbfmt(sb, "uint32")
	| `Tyuint:	std.sbfmt(sb, "uint")
	| `Tyuint64:	std.sbfmt(sb, "uint64")
	/*floats */
	| `Tyflt32:	std.sbfmt(sb, "flt32")
	| `Tyflt64:	std.sbfmt(sb, "flt64")
	/* a bit odd.. */
	| `Tyvalist:	std.sbfmt(sb, "...")
	/* compound types */
	| `Tyslice sub:	std.sbfmt(sb, "{}[:]", sub)
	| `Typtr sub:	std.sbfmt(sb, "{}#", sub)
	| `Tyarray (sub, `std.Some len):	std.sbfmt(sb, "{}[{}]", sub, len)
	| `Tyarray (sub, `std.None):	std.sbfmt(sb, "{}[...]", sub)
	| `Tystruct dcls:	structdump(sb, dcls)
	| `Tyunion dcls:	uniondump(sb, dcls)
	| `Tytuple args:	parendump(sb, args, `std.None)
	| `Tyfunc args:		parendump(sb, args[1:], `std.Some args[0])
	/* user defined types */
	| `Tygeneric (n, args, sub):	nameddump(sb, [.ns="", .name=n], args)
	| `Tyname (n, args, sub):	nameddump(sb, n, args)
	| `Tyunres (n, args):	nameddump(sb, n, args)
	| `Typaram n:	std.sbfmt(sb, "@{}", n)
	| `Tyvar id:	std.sbfmt(sb, "${}", id)
	;;
}

const parendump = {sb, args, ret
	var sep

	sep = ""
	std.sbputs(sb, "(")
	for arg in args
		std.sbputs(sb, sep)
		tydefdump(sb, arg)
		sep = ", "
	;;
	match ret
	| `std.None:
		/* nothing */
	| `std.Some a:
		if args.len > 0
			std.sbputs(sb, " ")
		;;
		std.sbputs(sb, "-> ")
		tydefdump(sb, a)
	;;
	std.sbputs(sb, ")")
}

const structdump = {sb, dcls
	std.sbfmt(sb, "struct\n")
	for d in dcls
		std.sbfmt(sb, "{}\t: {}\n", d.name, d.ty)
	;;
	std.sbfmt(sb, ";;")
}

const uniondump = {sb, ucons
	std.sbfmt(sb, "union\n")
	for uc in ucons
		std.sbfmt(sb, "`{} {}\n", uc.tag, uc.ety)
	;;
	std.sbfmt(sb, ";;")
}

const nameddump = {sb, n : name, args
	var sep

	sep = ""
	if n.ns.len > 0
		std.sbfmt(sb, "{}.", n.ns)
	;;
	std.sbfmt(sb, "{}", n.name)
	if args.len > 0
		std.sbfmt(sb, "(")
		for a in args
			std.sbfmt(sb, "{}{}", sep, a)
			sep = ", "
		;;
		std.sbfmt(sb, ")")
	;;
}

const litdump = {sb, lit, ind
	match lit
	| `Lint	(v, _, _):	ifmt(sb, ind, "{}", v)
	| `Lflt	v:	ifmt(sb, ind, "{}", v)
	| `Lchr	v:	ifmt(sb, ind, "{}", v)
	| `Lstr	v:	ifmt(sb, ind, "\"{e}\"", v)
	| `Llbl	v:	ifmt(sb, ind, "{}", v)
	| `Lbool v:	ifmt(sb, ind, "{}", v)
	| `Lfunc v:	fndump(sb, v, ind)
	| `Lvoid:	ifmt(sb, ind, "void")
	;;
}

const fndump = {sb, fn, ind
	ifmt(sb, ind, "func {{\n")
	ifmt(sb, ind + 1, "loc: {}\n", fn.loc)
	ifmt(sb, ind + 1, "type: {}\n", fn.ty)
	ifmt(sb, ind + 1, "args: [\n")
	for a in fn.args
		dcldump(sb, a, ind + 2)
	;;
	ifmt(sb, ind + 1, "]\n")
	stabdump(sb, fn.stab, ind + 1)
	blkdump(sb, fn.body, ind + 1, false)
	ifmt(sb, ind, "}}\n")
}

const blkdump = {sb, blk, ind, showstab
	ifmt(sb, ind, "block {{\n")
	if showstab
		stabdump(sb, blk.stab, ind + 1)
	;;
	for s in blk.stmts
		stmtdump(sb, s, ind + 1)
		std.sbputs(sb, "\n")
	;;
	ifmt(sb, ind, "}}\n")
}

const stmtdump = {sb, stmt, ind
	match stmt
	| &(`Nbreak):	ifmt(sb, ind, "break\n")
	| &(`Ncont):	ifmt(sb, ind, "cont\n")
	| &(`Nlbl l):	ifmt(sb, ind, ":{}\n", l)
	| &(`Ngoto l): 	ifmt(sb, ind, "goto {}\n", l)
	| &(`Nexpr s):	exprdump(sb, s, ind)
	| &(`Nloop s):	loopdump(sb, s, ind)
	| &(`Niter s):	iterdump(sb, s, ind)
	| &(`Nif s):	ifdump(sb, s, ind)
	| &(`Nmatch s):	matchdump(sb, s, ind)
	| &(`Ndcl s):	dcldump(sb, s, ind)
	;;
}

const loopdump = {sb, s, ind
	ifmt(sb, ind, "Loop\n")
	ifmt(sb, ind + 1, "init:\t{}\n", s.init)
	ifmt(sb, ind + 1, "cond:\t{}\n", s.cond)
	ifmt(sb, ind + 1, "step:\t{}\n", s.step)
	blkdump(sb, s.body, ind + 1, true)
	std.sbputs(sb, "\n")
}

const iterdump = {sb, s, ind
	ifmt(sb, ind, "Loop\n")
	ifmt(sb, ind + 1, "pat:\t{}\n", s.pat)
	ifmt(sb, ind + 1, "iter:\t{}\n", s.iter)
	blkdump(sb, s.body, ind + 1, true)
	std.sbputs(sb, "\n")
}

const ifdump = {sb, s, ind
	ifmt(sb, ind, "If\n")
	ifmt(sb, ind + 1, "cond:\t{}\n", s.cond)
	ifmt(sb, ind + 1, "iftrue:\n")
	blkdump(sb, s.iftrue, ind + 2, true)
	ifmt(sb, ind + 1, "iffalse:\n")
	blkdump(sb, s.iffalse, ind + 2, true)
	std.sbputs(sb, "\n")
}

const matchdump = {sb, s, ind
	ifmt(sb, ind, "Match\n")
	ifmt(sb, ind + 1, "val: {}\n", s.val)
	for c in s.cases
		ifmt(sb, ind + 1, "pat: {}\n", c.pat)
		ifmt(sb, ind + 1, "action:\n")
		blkdump(sb, c.act, ind + 2, true)
	;;
	std.sbputs(sb, "\n")
}
