use std

use "ast"
use "stab"
use "tok"
use "tokdefs"
use "type"
use "types"
use "node"
use "util"

pkg parse =
	const tsfile	: (ts : tokstream# -> file#)
;;

const tsfile = {ts
	var f
	var t : tok
	var vis : vis = `Visintern

	/* initialize the parser and file state */
	f = std.mk([
		.uses = [][:],
		.libs = [][:],
		.extlibs = [][:],
		.dcls = [][:],
		.extinit = [][:],
		.init = `std.None,
		.globls = mkstab(),
		.builtin = mkstab(),
		.ns = std.mkht(std.strhash, std.streq),
	])
	f.globls.super = `std.Some f.builtin

	/* parse */
	optendlns(ts)
	while true
		match tokpeek(ts)
		| (l, `Teof):	break
		| (l, `Tuse):	usestmt(ts, f)
		| (l, `Tpkg):	pkgdef(ts, f)
		| (l, `Ttrait):	traitdef(ts, f)
		| (l, `Ttype):	tydef(ts, f)
		| (l, `Tendln):	endlns(ts)
		| (l, tok):	
			if !vardcl(ts, f, true, vis)
				err(l, "invalid top level item near {}", tok)
			;;
		;;
	;;
	-> f
}

const usestmt = {ts, f
	match toknext(ts)
	| (l, `Tuse): /* ok */
	| (l, t):	err(l, "unexpected token in use {}\n", t)
	;;

	match toknext(ts)
	| (l, `Tstrlit str):	std.slpush(&f.uses, `Ulocal str)
	| (l, `Tident id):	std.slpush(&f.uses, `Ulib id)
	| (l, t):	err(l, "unexpected {} after use\n", t)
	;;
}

const pkgdef = {ts, f
	std.fatal("unimplemented pkg\n")
}

const traitdef = {ts, f
	std.fatal("unimplemented trait\n")
}

const tydef = {ts, f
	std.fatal("unimplemented type\n")
}

const vardcl = {ts, f, isglobl, vis
	var dcl : dcl
	var hadattr

	/* attributes */
	dcl = [
		.id = f.dcls.len,
		.vis = vis,
		.init = `std.None,
		.tr = `std.None,
		.isglobl = isglobl,
	]

	hadattr = dclattrs(ts, &dcl)
	/* attributes (const | var) */
	match tokpeek(ts)
	| (l, `Tvar):	dcl.isconst = false
	| (l, `Tconst):	dcl.isconst = true
	| (l, `Tgeneric):	
		dcl.isconst = true
		dcl.isgeneric = true
	| (l, badtok):
		if hadattr
			err(l, "unexpected {} in declaration, got {}\n", badtok)
		else
			-> false
		;;
	;;
	if !dcltag(ts, &dcl)
		if hadattr
		else
			-> false
		;;
	;;
	dclname(ts, &dcl)
	dcltype(ts, &dcl)
	dclinit(ts, &dcl)
	std.slpush(&f.dcls, std.mk(dcl))
	-> true
}


const dclname = {ts, dcl : dcl#
	/* attributes (const | var) name */
	match name(ts)
	| `std.Some n:
		if n.ns.len > 0 && !dcl.isextern
			std.fatal("only extern decls may provide namespaces\n")
		;;
		dcl.name = n
	| `std.None:	
		std.fatal("expected name in decl, got {}\n", tokpeekt(ts))
	;;
}

const dcltype = {ts, dcl
	match tokpeek(ts)
	| (l, `Tcolon):
		toknext(ts)
		match typ(ts)
		| `std.Some t:	dcl.ty = t
		| `std.None:	err(l, "expected type after ':'\n")
		;;
	| (l, _):
		dcl.ty = mktyvar(l)
	;;
}

const dclinit = {ts, dcl
	match tokpeek(ts)
	| (l, `Tasn):
		toknext(ts)
		match expr(ts)
		| `std.Some e:	dcl.init = `std.Some e
		| `std.None:	err(l, "expected initializer after '='\n")
		;;
		-> true
	| tok:	
		dcl.init = `std.None
	;;

	-> false
}

const expr = {ts
	match unionexpr(ts)
	| `std.Some e:	-> `std.Some exprcore(ts, e, 0)
	| `std.None:	-> `std.None
	;;
}

const exprcore = {ts, lhs, minprec
	var peek, peekloc, assoc
	var prec, peekprec
	var op, oploc
	var rhs

	(peekloc, peek) = tokpeek(ts)
	while true
		(prec, assoc) = exprprec(peek) 
		if prec < minprec
			break
		;;

		op = peek
		oploc = peekloc
		toknext(ts)

		match unionexpr(ts)
		| `std.Some e:	rhs = e
		| `std.None:	err(ts.loc, "expected primary expression after {}, near {}", op, tokpeekt(ts))
		;;

		(peekloc, peek) = tokpeek(ts)
		while true
			(peekprec, assoc) = exprprec(peek)
			if (!assoc && peekprec <= prec) || (assoc && peekprec < prec)
				break
			;;
			rhs = binexprcore(ts, rhs, peekprec)
			(peekloc, peek) = tokpeek(ts)
		;;
		match op
		/* assignmets */
		| `Tasn:	lhs = mkexpr(oploc, `Oasn (lhs, rhs))
		| `Taddeq:	lhs = mkexpr(oploc, `Oaddeq (lhs, rhs))
		| `Tsubeq:	lhs = mkexpr(oploc, `Osubeq (lhs, rhs))
		| `Tmuleq:	lhs = mkexpr(oploc, `Omuleq (lhs, rhs))
		| `Tdiveq:	lhs = mkexpr(oploc, `Odiveq (lhs, rhs))
		| `Tmodeq:	lhs = mkexpr(oploc, `Omodeq (lhs, rhs))
		| `Tboreq:	lhs = mkexpr(oploc, `Oboreq (lhs, rhs))
		| `Tbandeq:	lhs = mkexpr(oploc, `Obandeq (lhs, rhs))
		| `Tbsleq:	lhs = mkexpr(oploc, `Obsleq (lhs, rhs))
		| `Tbsreq:	lhs = mkexpr(oploc, `Obsreq (lhs, rhs))
		/* logical ops */
		| `Tlor:	lhs = mkexpr(oploc, `Olor (lhs, rhs))
		| `Tland:	lhs = mkexpr(oploc, `Oland (lhs, rhs))
		/* multiplicative */
		| `Teq:	lhs = mkexpr(oploc, `Oeq (lhs, rhs))
		| `Tgt:	lhs = mkexpr(oploc, `Ogt (lhs, rhs))
		| `Tge:	lhs = mkexpr(oploc, `Oge (lhs, rhs))
		| `Tlt:	lhs = mkexpr(oploc, `Olt (lhs, rhs))
		| `Tle:	lhs = mkexpr(oploc, `Ole (lhs, rhs))
		| `Tne:	lhs = mkexpr(oploc, `One (lhs, rhs))
		| _:	std.die("broken parser")
		;;
	;;
	-> lhs
}

const exprprec = {tok
	match tok
	/* assignmets */
	| `Tasn:	-> (0, true)
	| `Taddeq:	-> (0, true)
	| `Tsubeq:	-> (0, true)
	| `Tmuleq:	-> (0, true)
	| `Tdiveq:	-> (0, true)
	| `Tmodeq:	-> (0, true)
	| `Tboreq:	-> (0, true)
	| `Tbandeq:	-> (0, true)
	| `Tbsleq:	-> (0, true)
	| `Tbsreq:	-> (0, true)
	/* logical ops */
	| `Tlor:	-> (1, false)
	| `Tland:	-> (2, false)
	/* multiplicative */
	| `Teq:	-> (3, false)
	| `Tgt:	-> (3, false)
	| `Tge:	-> (3, false)
	| `Tlt:	-> (3, false)
	| `Tle:	-> (3, false)
	| `Tne:	-> (3, false)
	/* all others */
	| _:	-> (-1, false)
	;;
}

const unionexpr = {ts
	var tag, expr

	match tokpeek(ts)
	| (l, `Ttick):
		toknext(ts)
		match name(ts)
		| `std.Some n:	tag = n
		| `std.None:	err(l, "expected union tag after '`'\n")
		;;

		expr = castexpr(ts)
		-> `std.Some mkexpr(l, `Oucon (tag, expr))
	| _:
		-> castexpr(ts)
	;;
}

const castexpr = {ts
	var ty

	match binexpr(ts)
	| `std.Some e:
		match tokpeek(ts)
		| (l, `Tcast):
			toknextt(ts)
			match toknext(ts)
			| (pl, `Toparen):
			| (pl, badtok):	err(l, "expected '(' after 'castto', got {}\n", badtok)
			;;

			match typ(ts)
			| `std.Some t:	ty = t
			| `std.None:	err(ts.loc, "expected type in castto near {}\n", tokpeekt(ts))
			;;

			match toknext(ts)
			| (pl, `Tcparen):
			| (pl, badtok):	err(l, "expected '(' after 'castto', got {}\n", badtok)
			;;

			-> `std.Some mkexpr(l, `Ocast (e, ty))
		| _:	
			-> `std.Some e
		;;
	| `std.None:
		-> `std.None
	;;
}

const binexpr = {ts
	match prefixexpr(ts)
	| `std.Some e:	-> `std.Some binexprcore(ts, e, 0)
	| `std.None:	-> `std.None
	;;
}

const binexprcore = {ts, lhs, minprec
	var prec, peekprec
	var op, oploc
	var peek, peekloc
	var rhs

	(peekloc, peek) = tokpeek(ts)
	while true
		prec = binprec(peek) 
		if prec < minprec
			break
		;;

		op = peek
		oploc = peekloc
		toknext(ts)

		match prefixexpr(ts)
		| `std.Some e:	rhs = e
		| `std.None:	err(ts.loc, "expected primary expression after {}, near {}", op, tokpeekt(ts))
		;;

		(peekloc, peek) = tokpeek(ts)
		while true
			peekprec = binprec(peek)
			if peekprec <= prec
				break
			;;
			rhs = binexprcore(ts, rhs, peekprec)
			(peekloc, peek) = tokpeek(ts)
		;;
		match op
		/* binary or */
		| `Tbor:	lhs = mkexpr(oploc, `Obor (lhs, rhs))
		| `Tbxor:	lhs = mkexpr(oploc, `Obxor (lhs, rhs))
		/* binary and */
		| `Tband:	lhs = mkexpr(oploc, `Oband (lhs, rhs))
		/* additive */
		| `Tplus:	lhs = mkexpr(oploc, `Oadd (lhs, rhs))
		| `Tminus:	lhs = mkexpr(oploc, `Osub (lhs, rhs))
		/* multiplicative */
		| `Tmul:	lhs = mkexpr(oploc, `Omul (lhs, rhs))
		| `Tdiv:	lhs = mkexpr(oploc, `Odiv (lhs, rhs))
		| `Tmod:	lhs = mkexpr(oploc, `Omod (lhs, rhs))
		/* shift */
		| `Tbsl:	lhs = mkexpr(oploc, `Obsl (lhs, rhs))
		| `Tbsr:	lhs = mkexpr(oploc, `Obsr (lhs, rhs))
		/* all others */
		| _:	std.die("broken parser")
		;;
	;;
	-> lhs
}

const binprec = {tok
	match tok
	/* binary or */
	| `Tbor:	-> 0
	| `Tbxor:	-> 0
	/* binary and */
	| `Tband:	-> 1
	/* additive */
	| `Tplus:	-> 2
	| `Tminus:	-> 2
	/* multiplicative */
	| `Tmul:	-> 3
	| `Tdiv:	-> 3
	| `Tmod:	-> 3
	/* shift */
	| `Tbsl:	-> 4
	| `Tbsr:	-> 3
	/* all others */
	| _:	-> -1
	;;
}

const prefixexpr = {ts
	match toknext(ts)
	| (l, `Tinc):
		match prefixexpr(ts)
		| `std.Some e:	-> `std.Some mkexpr(l, `Opreinc e)
		| `std.None:	-> `std.None
		;;
	| (l, `Tdec):
		match prefixexpr(ts)
		| `std.Some e:	-> `std.Some mkexpr(l, `Opredec e)
		|`std.None:	-> `std.None
		;;
	| (l, `Tband):
		match prefixexpr(ts)
		| `std.Some e:	-> `std.Some mkexpr(l, `Oaddr e)
		|`std.None:	-> `std.None
		;;
	| (l, `Tlnot):
		match prefixexpr(ts)
		| `std.Some e:	-> `std.Some mkexpr(l, `Olnot e)
		|`std.None:	-> `std.None
		;;
	| (l, `Tbnot):
		match prefixexpr(ts)
		| `std.Some e:	-> `std.Some mkexpr(l, `Obnot e)
		|`std.None:	-> `std.None
		;;
	| (l, `Tminus):
		match prefixexpr(ts)
		| `std.Some e:	-> `std.Some mkexpr(l, `Oneg e)
		|`std.None:	-> `std.None
		;;
	| (l, `Tplus):
		match prefixexpr(ts)
		| `std.Some e:	-> `std.Some e
		|`std.None:	-> `std.None
		;;
	| _:	
		tokunget(ts)
		-> postfixexpr(ts)
	;;
}

const postfixexpr = {ts
	var e

	match atomicexpr(ts)
	| `std.Some r:	e = r
	| `std.None:	-> `std.None
	;;

	while true
		match toknext(ts)
		| (l, `Tdot):	
			match toknext(ts)
			| (ml, `Tident memb):	e = mkexpr(l, `Omemb (e, memb))
			| (ml, badtok):	err(ml, "expected member after '.', got {}\n", badtok)
			;;
		| (l, `Tinc):	e = mkexpr(l, `Opostinc e)
		| (l, `Tdec):	e = mkexpr(l, `Opostdec e)
		| (l, `Tderef):	e = mkexpr(l, `Oderef e)
		| (l, `Tosqbrac):	e = idxexpr(ts, e)
		| (l, `Toparen):	e = callexpr(ts, e)
		| tok:	
			tokunget(ts)
			break
		;;
	;;
	-> `std.Some e
}

const idxexpr = {ts : tokstream#, base
	var hadsl
	var lo, hi
	var loc

	loc = ts.loc

	/* open [ is consumed by caller */

	match expr(ts)
	| `std.Some e:	lo = `std.Some e
	| `std.None:	lo = `std.None
	;;

	hadsl = false
	match tokpeek(ts)
	| (l, `Tcolon):
		toknext(ts)
		hadsl = true
		match expr(ts)
		| `std.Some e:	hi = `std.Some e
		| `std.None:	hi = `std.None
		;;
	| (l, tok):
		match lo
		| `std.Some _:	/* ok */
		| `std.None:	err(l, "expected index in expr, got {}\n", tok)
		;;

	;;

	match toknext(ts)
	| (_, `Tcsqbrac):	/* ok */
	| (l, badtok):	err(l, "expected ']' after indexer, got {}\n", badtok)
	;;

	if hadsl
		-> mkexpr(loc, `Oslice (base, lo, hi))
	else
		match lo
		| `std.Some e:	-> mkexpr(loc, `Oidx (base, e))
		| `std.None:	std.die("should be unreachable\n")
		;;
	;;

}

const callexpr = {ts, e
	var args
	/* open ) is consumed by caller */

	args = callargs(ts)

	match toknext(ts)
	| (_, `Tcparen):	/* ok */
	| (l, badtok):	err(l, "expected ')' after call, got {}\n", badtok)
	;;
	
	-> mkexpr(ts.loc, `Ocall (e, args))

}

const callargs = {ts
	var args

	args = [][:]
	match expr(ts)
	| `std.None:	-> args
	| `std.Some e:	std.slpush(&args, e)
	;;

	while true
		match toknext(ts)
		| (l, `Tcomma):
			match expr(ts)
			| `std.Some e:	std.slpush(&args, e)
			| `std.None:	err(l, "expected expression after ',' in function args\n")
			;;
		| _:
			tokunget(ts)
			break
		;;
	;;
	-> args
}

const atomicexpr = {ts
	var e

	match toknext(ts)
	| (l, `Tgap):		e = `std.Some mkexpr(l, `Ogap)
	| (l, `Tident n):	e = `std.Some mkexpr(l, `Ovar [.ns="", .name=n])
	| (l, `Tintlit i):	e = `std.Some mkexpr(l, `Olit `Lint i)
	| (l, `Tstrlit s):	e = `std.Some mkexpr(l, `Olit `Lstr s)
	| (l, `Tfltlit f):	e = `std.Some mkexpr(l, `Olit `Lflt f)
	| (l, `Tchrlit c):	e = `std.Some mkexpr(l, `Olit `Lchr c)
	| (l, `Tboollit b):	e = `std.Some mkexpr(l, `Olit `Lbool b)
	| (l, `Tvoidlit):	e = `std.Some mkexpr(l, `Olit `Lvoid)
	| (l, `Tsizeof):	e = sizeofexpr(ts, l)
	| (l, `Tobrace):	e = funcexpr(ts, l)
	| _:
		tokunget(ts)
		e = `std.None
	;;

	-> e
}

const sizeofexpr = {ts, loc
	var e

	match toknext(ts)
	| (_, `Toparen):	/* ok */
	| (l, badtok):	err(l, "expected '(' after sizeof, got {}\n", badtok)
	;;

	match typ(ts)
	| `std.Some ty:	e = mkexpr(loc, `Osize ty)
	| `std.None:	err(ts.loc, "expected type in sizeof, got {}\n", tokpeekt(ts))
	;;

	match toknext(ts)
	| (_, `Toparen):	/* ok */
	| (l, badtok):	err(l, "expected '(' after sizeof, got {}\n", badtok)
	;;

	-> `std.Some e
}

const funcexpr = {ts, loc
	-> `std.None
}

const dclattrs = {ts, dcl
	var hadattr

	hadattr = false
	while true
		match tokpeek(ts)
		| (_, `Tattr `Attrpkglocal):	dcl.ispkglocal = true
		| (_, `Tattr `Attrextern):	dcl.isextern = true
		| (_, `Tattr `Attrnoret):	dcl.isnoret = true
		| _:	break
		;;
		toknext(ts)
		hadattr = true
	;;
	-> hadattr
}

const dcltag = {ts, dcl
	/* attributes (const | var) */
	match tokpeek(ts)
	| (_, `Tvar):	dcl.isconst = false
	| (_, `Tconst):	dcl.isconst = true
	| (_, `Tgeneric):	
		dcl.isconst = true
		dcl.isgeneric = true
	| (l, badtok):
		-> false
	;;
	toknext(ts)
	-> true
}

const name = {ts -> std.option(name)
	var ns, n

	match tokpeek(ts)
	| (l, `Tident id):	n = id
	| (l, tok):	-> `std.None
	;;
	toknext(ts)

	match tokpeek(ts)
	| (l, `Tdot):	ns = n
	| (l, _):	-> `std.Some [.ns = "", .name = n]
	;;
	toknext(ts)

	match tokpeek(ts)
	| (l, `Tident id):	n = id
	| (l, tok):	err(l, "expected after '.', got {}\n", tok)
	;;
	toknext(ts)

	-> `std.Some [.ns=ns, .name=n]
}

const typ = {ts
	var ty

	match toknext(ts)
	| (l, `Tstruct):	ty = tystruct(ts)
	| (l, `Tunion):		ty = tyunion(ts)
	| (l, `Tellipsis):	ty = mkty(l, `Tyvalist)
	| (l, `Ttyparam n):	ty = mkty(l, `Typaram n)
	| (l, `Tident id):
		tokunget(ts)
		ty = tyname(ts)
	| (l, badtok):	
		tokunget(ts)
		-> `std.None
	;;

	-> `std.Some tysuffixed(ts, ty)
}

const tystruct = {ts
	/* already have struct token */
	err(ts.loc, "struct not yet supported\n")
	-> std.alloc()
}

const tyunion = {ts
	/* already have union token */
	err(ts.loc, "struct not yet supported\n")
	-> std.alloc()
}

const tyname : (ts : tokstream# -> tydef#) = {ts
	var nam, params

	match name(ts)
	| `std.Some n:	nam = n
	| `std.None:	std.die("we came hear after peeking an ident\n")
	;;

	params = [][:]
	match tokpeek(ts)
	| (l, `Toparen):	err(l, "param lists aren't yet supported\n")
	| (l, _):
	;;

	-> mkty(ts.loc, `Tyunres (nam, params))
}

const tysuffixed = {ts, base
	while true
		match toknext(ts)
		| (l, `Tderef):	base = mkty(l, `Typtr base)
		| (l, `Tosqbrac):
			match tokpeekt(ts)
			| `Tcolon:
				toknext(ts)
				base = mkty(l, `Tyslice base)
				expectcsqbrac(ts, "in slice type")
			| _:
				match expr(ts)
				| `std.Some e:	base = mkty(l, `Tyarray (base, e))
				| `std.None:	err(ts.loc, "expected expression in array size\n")
				;;
				expectcsqbrac(ts, "in array type")
			;;
		| _:
			tokunget(ts)
			break
		;;
	;;
	-> base
}

const expectcsqbrac = {ts, msg
	match toknext(ts)
	| (l, `Tcsqbrac):	/* nothing */
	| (l, tok):	err(l, "expected ']' in {}, got {}\n", msg, tok)
	;;
}

const optendlns = {ts
	while true
		match tokpeek(ts)
		| (l, `Tendln): toknext(ts)
		| _:    break
		;;
	;;
}

const endlns = {ts
	match tokpeek(ts)
	| (l, `Tendln): optendlns(ts)
	| (l, tok):	err(l, "expected \\n, got {}\n", tok)
	;;
	-> true
}
